<!doctype html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <title>showcase</title>
    <script id="glsl/vs-uniform-color.c" type="x-shader/x-vertex">
//
// vs-uniform-color.c
//
// Reed College CSCI 385 Computer Graphics Fall 2024
//
// Simple vertex shader that preprocesses per-vertex information for a
// fragment shader. It uses the same color information uniformly for
// all the vertices of the object.
//
// It calculates a position using the supplied vertex positions and the two
// standard WebGL transforation matrices.
//
// It sends the color information to the fragment shader.
//
attribute vec4 aVertexPosition;

uniform mat4 uModelViewMatrix;
uniform mat4 uProjectionMatrix;
uniform vec4 uColor;

varying vec4 color;

void main(void) {
  gl_Position = uProjectionMatrix * uModelViewMatrix * aVertexPosition;
  color = uColor;
}
    </script>
    <script id="glsl/vs-varying-color.c" type="x-shader/x-vertex">
//
// vs-varying-color.c
//
// Reed College CSCI 385 Computer Graphics Fall 2024
//
// Simple vertex shader that preprocesses per-vertex information for a
// fragment shader. It expects the color information to vary amongst
// the vertices of the object.
//
// It calculates a position using the supplied vertex positions and the two
// standard WebGL transforation matrices.
//
// It sends the (interpolated) color information to the fragment shader.
//
attribute vec4 aVertexPosition;
attribute vec4 aVertexColor;

uniform mat4 uModelViewMatrix;
uniform mat4 uProjectionMatrix;

varying vec4 color;

void main(void) {
  gl_Position = uProjectionMatrix * uModelViewMatrix * aVertexPosition;
  color = aVertexColor;
}
    </script>
    <script id="glsl/fs-color.c" type="x-shader/x-fragment">
//
// fs-color.c
//
// Reed College CSCI 385 Computer Graphics Fall 2024
//
// Simple fragment shader that gets fed a color from the vertex shader.
// Nothing else is communicated to it from the WebGL program.
//
varying lowp vec4 color;

void main(void) {
  gl_FragColor = color;
}
    </script>
    <script id="glsl/vs-uniform-material.c" type="x-shader/x-vertex">
//
// vs-uniform-material.c
//
// Reed College CSCI 385 Computer Graphics Fall 2024
//
// Vertex shader that preprocesses per-vertex information to be fed
// into a Phong fragment shader. It expects the material's color
// information to be uniform over all the vertices of the object.
//
// It sends this (interpolated) information to the fragment shader: 
// * The position of a fragment of a facet or line object defined by
//   several vertex positions.
// * The normal of that surface.
// * The color of the material.
//
// It is fed per-vertex information with attributes for:
// * vertex position
// * surface normal at that vertex
// * material color at that vertex
//
attribute vec4 aVertexPosition;   // Corner of some facet of the surface.
attribute vec4 aVertexNormal;     // Surface normal at that osition.

uniform mat4 uProjectionMatrix;
uniform mat4 uModelViewMatrix;
uniform vec4 uMaterialColor;      // Color of material.

varying vec4 position;   // Fragment's surface position.
varying vec4 normal;     // Fragment's surface normal.
varying vec4 material;   // Fragment surface's material color. 

mat3 transpose(mat3 m) {
  mat3 mt = mat3(vec3(m[0].x, m[1].x, m[2].x),
                 vec3(m[0].y, m[1].y, m[2].y),
                 vec3(m[0].z, m[1].z, m[2].z));
  return mt;
}

// The code below is from
//    https://stackoverflow.com/questions/53378692/glsl-1-3-inverse-4x4-matrix
// See also
//    https://ardoris.wordpress.com/2008/07/18/general-formula-for-the-inverse-of-a-3x3-matrix/
        
float det(mat2 matrix) {
    return matrix[0].x * matrix[1].y - matrix[0].y * matrix[1].x;
}
    
mat3 inverse(mat3 matrix) {
    vec3 row0 = matrix[0];
    vec3 row1 = matrix[1];
    vec3 row2 = matrix[2];

    vec3 minors0 = vec3(
        det(mat2(row1.y, row1.z, row2.y, row2.z)),
        det(mat2(row1.z, row1.x, row2.z, row2.x)),
        det(mat2(row1.x, row1.y, row2.x, row2.y))
    );
    vec3 minors1 = vec3(
        det(mat2(row2.y, row2.z, row0.y, row0.z)),
        det(mat2(row2.z, row2.x, row0.z, row0.x)),
        det(mat2(row2.x, row2.y, row0.x, row0.y))
    );
    vec3 minors2 = vec3(
        det(mat2(row0.y, row0.z, row1.y, row1.z)),
        det(mat2(row0.z, row0.x, row1.z, row1.x)),
        det(mat2(row0.x, row0.y, row1.x, row1.y))
    );

    mat3 adj = transpose(mat3(minors0, minors1, minors2));

    return (1.0 / dot(row0, minors0)) * adj;
}

void main() {
  
  // Transform and interpolate vertex information.
  vec4 proj_position = uProjectionMatrix * uModelViewMatrix * aVertexPosition;
  
  position = uModelViewMatrix * aVertexPosition;
  vec3 n3 = vec3(aVertexNormal.x, aVertexNormal.y, aVertexNormal.z);
  vec3 tn3 = normalize(inverse(transpose(mat3(uModelViewMatrix))) * n3);
  normal   = vec4(tn3,0.0);
  material   = uMaterialColor;
 
  // The output required by GLSL.
  gl_Position = proj_position;
}

    </script>
    <script id="glsl/vs-varying-material.c" type="x-shader/x-vertex">
//
// vs-varying-material.c
//
// Reed College CSCI 385 Computer Graphics Fall 2024
//
// Vertex shader that preprocesses per-vertex information to be fed
// into a Phong fragment shader. It expects the material's color
// information to vary amongst the vertices of the object.
//
// It sends this (interpolated) information to the fragment shader:
// * The position of a fragment of a facet or line object defined by
//   several vertex positions.
// * The normal of that surface.
// * The color of the material.
//
// It is fed per-vertex information with attributes for:
// * vertex position
// * surface normal at that vertex
// * material color at that vertex
//
attribute vec4 aVertexPosition;   // Corner of some facet of the surface.
attribute vec4 aVertexNormal;     // Surface normal at that position.
attribute vec4 aVertexMaterial;   // Color of material at that position.

uniform mat4 uProjectionMatrix;
uniform mat4 uModelViewMatrix;

varying vec4 position;   // Fragment's surface position.
varying vec4 normal;     // Fragment's surface normal.
varying vec4 material;   // Fragment surface's material color.

mat3 transpose(mat3 m) {
  mat3 mt = mat3(vec3(m[0].x, m[1].x, m[2].x),
                 vec3(m[0].y, m[1].y, m[2].y),
                 vec3(m[0].z, m[1].z, m[2].z));
  return mt;
}

// The code below is from
//    https://stackoverflow.com/questions/53378692/glsl-1-3-inverse-4x4-matrix
// See also
//    https://ardoris.wordpress.com/2008/07/18/general-formula-for-the-inverse-of-a-3x3-matrix/
        
float det(mat2 matrix) {
    return matrix[0].x * matrix[1].y - matrix[0].y * matrix[1].x;
}
    
mat3 inverse(mat3 matrix) {
    vec3 row0 = matrix[0];
    vec3 row1 = matrix[1];
    vec3 row2 = matrix[2];

    vec3 minors0 = vec3(
        det(mat2(row1.y, row1.z, row2.y, row2.z)),
        det(mat2(row1.z, row1.x, row2.z, row2.x)),
        det(mat2(row1.x, row1.y, row2.x, row2.y))
    );
    vec3 minors1 = vec3(
        det(mat2(row2.y, row2.z, row0.y, row0.z)),
        det(mat2(row2.z, row2.x, row0.z, row0.x)),
        det(mat2(row2.x, row2.y, row0.x, row0.y))
    );
    vec3 minors2 = vec3(
        det(mat2(row0.y, row0.z, row1.y, row1.z)),
        det(mat2(row0.z, row0.x, row1.z, row1.x)),
        det(mat2(row0.x, row0.y, row1.x, row1.y))
    );

    mat3 adj = transpose(mat3(minors0, minors1, minors2));

    return (1.0 / dot(row0, minors0)) * adj;
}

void main() {
  
  // Transform and interpolate vertex information.
  vec4 proj_position = uProjectionMatrix * uModelViewMatrix * aVertexPosition;
  
  position = uModelViewMatrix * aVertexPosition;
  vec3 n3 = vec3(aVertexNormal.x, aVertexNormal.y, aVertexNormal.z);
  vec3 tn3 = normalize(inverse(transpose(mat3(uModelViewMatrix))) * n3);
  normal   = vec4(tn3,0.0);
  material = aVertexMaterial;
 
  // The output required by GLSL.
  gl_Position = proj_position;
}
    </script>
    <script id="glsl/fs-phong.c" type="x-shader/x-fragment">
//
// fs-phong.c
//
// Reed College CSCI 385 Computer Graphics Fall 2024
//
// Fragment shader that performs a variant of Phong shading.
//
// It is fed position, material color, and normal info from the vertex shader.
// It is also fed the following uniform information from the WebGL program:
//  * The color of the ambient light.
//  * Characteristics of up to three light sources (LIGHT0/1/2), namely:
//    + its color
//    + its position
//    + whether (the specular component of) that light is on/off
//  * The reflectance characteristics of the matrial, namely:
//    + how much of it is diffuse
//    + how much of it is specular
//    + how shiny the surface is ("shininess" as employed by the Phong model)
// It uses all this info to calculate the fragment color at its surface point.
//

precision highp float;

varying vec4 position;   // Fragment's surface position.
varying vec4 normal;     // Fragment's surface normal.
varying vec4 material;   // Fragment surface's material color.

uniform int  uLight0Enabled;  // Is the light on?
uniform vec4 uLight0Position; // Location of the light.
uniform vec4 uLight0Ambient;  // Color of the ambient component of the light.
uniform vec4 uLight0Diffuse;  // Color of the diffuse component of the light.
uniform vec4 uLight0Specular; // Color of the specular component of the light.
//
uniform int  uLight1Enabled;  // Is the light on?
uniform vec4 uLight1Position; // Location of the light.
uniform vec4 uLight1Ambient;  // Color of the ambient component of the light.
uniform vec4 uLight1Diffuse;  // Color of the diffuse component of the light.
uniform vec4 uLight1Specular; // Color of the specular component of the light.
//
uniform int  uLight2Enabled;  // Is the light on?
uniform vec4 uLight2Position; // Location of the light.
uniform vec4 uLight2Ambient;  // Color of the ambient component of the light.
uniform vec4 uLight2Diffuse;  // Color of the diffuse component of the light.
uniform vec4 uLight2Specular; // Color of the specular component of the light.
    
uniform float uMaterialDiffuse;   // Portion of reflection that's diffuse.
uniform float uMaterialSpecular;  // Portion of reflection that's specular.
uniform float uMaterialShininess; // Specular highlight control.

vec4 eye   = vec4(0.0,0.0,0.0,0.0);
    
vec4 phong_calc(vec4 light_position,
                vec4 light_ambient,
                vec4 light_diffuse,
                vec4 light_specular) {
  vec4  l = normalize(light_position - position);
  vec4  e = normalize(eye - position);
  vec4  n = normalize(normal);
  vec4  r = -l + 2.0 * dot(l,n) * n;
  float p = pow(max(dot(e,r),0.0),uMaterialShininess);
  
  vec4 ambient  = light_ambient * material;
  vec4 diffuse  = uMaterialDiffuse * light_diffuse * material * max(dot(l,n), 0.0);
  vec4 specular = uMaterialSpecular * light_specular * p * max(dot(l,n), 0.0);
  return ambient + diffuse + specular;
}
         
void main() {
  vec4 color = vec4(0.0,0.0,0.0,0.0);
  if (uLight0Enabled > 0) {
      color = color + phong_calc(uLight0Position,
                                 uLight0Ambient,
                                 uLight0Diffuse,
                                 uLight0Specular);
  }
  if (uLight1Enabled > 0) {
      color = color + phong_calc(uLight1Position,
                                 uLight1Ambient,
                                 uLight1Diffuse,
                                 uLight1Specular);
  }
  if (uLight2Enabled > 0) {
      color = color + phong_calc(uLight2Position,
                                 uLight2Ambient,
                                 uLight2Diffuse,
                                 uLight2Specular);
  }
  color.a = 1.0;
  gl_FragColor = color;    
}
    </script>

    <script src="gl-matrix-min.js" defer></script>
    <script src="gl.js" defer></script>
    <script src="load-shaders.js" defer></script>
    <script src="legacy-opengl.js" defer></script>
    <script src="geometry-3d.js" defer></script>
    <script src="quat.js" defer></script>
    <script src="showcase.js" defer></script>
    <script></script> 
  </head>

  <body>
    <canvas id="glcanvas" width="360" height="360"></canvas><br>
    <button onClick="glButtonAsKey('1')">1</button>
    <button onClick="glButtonAsKey('2')">2</button>
    <button onClick="glButtonAsKey('3')">3</button>
    <button onClick="glButtonAsKey('4')">4</button>
    <button onClick="glButtonAsKey('5')">5</button>
    <button onClick="glButtonAsKey('6')">6</button>
    <button onClick="glButtonAsKey('7')">7</button>
    <button onclick="glButtonAsKey('8')">8</button>
    <p>Smoothness (when applicable)</p>

    <p>Other params if needed</p>
  </body>
</html>
